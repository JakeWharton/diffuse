apply plugin: 'java-library'
apply plugin: 'org.jetbrains.kotlin.jvm'
//apply plugin: "com.vanniktech.maven.publish"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

configurations {
  r8
}

dependencies {
  implementation deps.clikt
  implementation deps.androidTools.dalvikDx
  implementation deps.picnic
  api deps.okio
  implementation deps.androidTools.binaryResources
  implementation deps.androidTools.apkSigner
  implementation deps.androidTools.bundleTool
  // TODO bundletool depends on aapt2-proto but with a runtime dependency rather than compile.
  //  https://github.com/google/bundletool/issues/177
  implementation deps.androidTools.aapt2Proto
  implementation deps.byteunits
  implementation deps.asm
  implementation deps.diffUtils

  r8 'com.android.tools:r8:2.0.99'

  testImplementation deps.junit
  testImplementation deps.truth
}

def fatJarProvider = tasks.register('fatJar', Jar) { task ->
  task.dependsOn(configurations.named('runtimeClasspath'))
  task.dependsOn(tasks.named('jar'))

  task.classifier 'fat'

  task.manifest {
    attributes 'Main-Class': 'com.jakewharton.diffuse.Diffuse'
    attributes 'Implementation-Version': version
  }

  def sourceClasses = sourceSets.main.output.classesDirs
  task.inputs.files(sourceClasses)

  task.doFirst {
    task.from files(sourceClasses)
    task.from configurations.runtimeClasspath.asFileTree.files.collect { zipTree(it) }

    task.exclude '**/*.kotlin_metadata'
    task.exclude '**/*.kotlin_module'
    task.exclude '**/*.kotlin_builtins'
    task.exclude '**/module-info.class'
    task.exclude 'META-INF/maven/**'
    task.exclude 'META-INF/proguard/**'
    task.exclude 'META-INF/*.version'
    task.exclude '**/*.proto'
    task.exclude 'LICENSE'
    task.exclude 'NOTICE'
    task.exclude 'r8-version.properties'
    task.exclude 'migrateToAndroidx/*'
  }
}

def r8File = new File("$buildDir/libs/$archivesBaseName-$version-r8.jar")
def r8JarProvider = tasks.register('r8Jar', JavaExec) { task ->
  def fatJar = fatJarProvider.get()
  def fatJarFile = fatJar.archivePath
  task.dependsOn(fatJar)
  task.inputs.file(fatJarFile)
  task.outputs.file(r8File)

  task.classpath(configurations.r8)
  task.main = 'com.android.tools.r8.R8'
  task.args = [
    '--release',
    '--classfile',
    '--output', r8File.toString(),
    '--pg-conf', 'src/main/rules.txt',
    '--lib', System.properties['java.home'].toString(),
    fatJarFile.toString(),
  ]
}

def binaryFile = new File(buildDir, "libs/$archivesBaseName-$version-binary.jar")
def binaryJar = tasks.register('binaryDiffuseJar') { task ->
  task.dependsOn(r8JarProvider)

  task.inputs.file(r8File)
  task.outputs.file(binaryFile)

  task.doLast {
    binaryFile.getParentFile().mkdirs()
    binaryFile.delete()
    binaryFile << "#!/bin/sh\n\nexec java \$JAVA_OPTS -jar \$0 \"\$@\"\n\n"
    r8File.withInputStream { binaryFile.append it }

    binaryFile.setExecutable true, false
  }
}

artifacts {
  archives file: binaryFile, name: 'binary', type: 'jar', builtBy: binaryJar, classifier: 'diffuse'
}
