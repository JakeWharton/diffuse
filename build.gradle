buildscript {
  ext.deps = [
      'androidTools': [
          'dalvikDx': 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3',
          'binaryResources': 'com.android.tools.apkparser:binary-resources:27.2.2',
          'apkSigner': 'com.android.tools.build:apksig:4.2.2',
          'bundleTool': 'com.android.tools.build:bundletool:1.6.1',
          // Keep this value in sync with bundletool's dependencies.
          'aapt2Proto': 'com.android.tools.build:aapt2-proto:4.2.2-7147631',
      ],
      'clikt': 'com.github.ajalt.clikt:clikt:3.5.0',
      'junit': 'junit:junit:4.13.2',
      'truth': 'com.google.truth:truth:1.1.3',
      'guava': 'com.google.guava:guava:31.1-jre',
      'okio': 'com.squareup.okio:okio:3.2.0',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'asm': 'org.ow2.asm:asm:9.3',
      'diffUtils': 'io.github.java-diff-utils:java-diff-utils:4.12',
      'picnic': 'com.jakewharton.picnic:picnic:0.6.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.10"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.7.10'
    classpath "org.jmailen.gradle:kotlinter-gradle:3.12.0"
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
  }

  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    google()
  }

  apply plugin: 'org.jmailen.kotlinter'

  plugins.withId('org.jetbrains.kotlin.jvm') {
    java {
      sourceCompatibility = JavaVersion.VERSION_1_8
      targetCompatibility = JavaVersion.VERSION_1_8
    }
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-Xjvm-default=all",
            "-progressive",
            '-opt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }
}
