plugins {
  alias(libs.plugins.kotlin.jvm) apply false
  alias(libs.plugins.kotlinter) apply false
  alias(libs.plugins.dokka) apply false
  alias(libs.plugins.mavenPublish) apply false
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  apply plugin: libs.plugins.kotlinter.get().pluginId

  plugins.withType(JavaBasePlugin).configureEach {
    java.toolchain {
      languageVersion.set(JavaLanguageVersion.of(11))
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      compilerOptions {
        freeCompilerArgs = [
            "-progressive",
            '-opt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency {
      if (requested.group == "com.android.tools.build" && requested.name == "aapt2-proto") {
        useVersion(libs.versions.aapt2Proto.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.protobuf" && requested.name == "protobuf-java") {
        useVersion(libs.versions.protobufJava.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.guava" && requested.name == "guava") {
        useVersion(libs.versions.guava.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
    }
  }
}
