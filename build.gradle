buildscript {
  dependencies {
    classpath libs.kotlinPlugin
    classpath libs.dokkaPlugin
    classpath libs.mavenPublishPlugin
    classpath libs.spotlessPlugin
    classpath libs.buildConfigPlugin
  }
  repositories {
    mavenCentral()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      ktlint().editorConfigOverride([
        'ktlint_standard_filename': 'disabled',
      ])
      target('**/*.kt')
    }
  }

  plugins.withType(JavaBasePlugin).configureEach {
    java.toolchain {
      languageVersion.set(JavaLanguageVersion.of(11))
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      compilerOptions {
        freeCompilerArgs = [
            "-progressive",
            '-opt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }

  configurations.configureEach {
    resolutionStrategy.eachDependency {
      if (requested.group == "com.android.tools.build" && requested.name == "aapt2-proto") {
        useVersion(libs.versions.aapt2Proto.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.protobuf" && requested.name == "protobuf-java") {
        useVersion(libs.versions.protobufJava.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
      if (requested.group == "com.google.guava" && requested.name == "guava") {
        useVersion(libs.versions.guava.get())
        because("We need to keep depenedencies in sync with bundletool")
      }
    }
  }
}
