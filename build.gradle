buildscript {
  ext.deps = [
      'androidTools': [
          'dalvikDx': 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3',
          'binaryResources': 'com.android.tools.apkparser:binary-resources:27.2.0-alpha08',
          'apkSigner': 'com.android.tools.build:apksig:4.2.0-alpha08',
          'bundleTool': 'com.android.tools.build:bundletool:1.2.0',
          // Keep this value in sync with bundletool's dependencies.
          'aapt2Proto': 'com.android.tools.build:aapt2-proto:4.1.0-alpha01-6193524',
      ],
      'clikt': 'com.github.ajalt:clikt:2.8.0',
      'junit': 'junit:junit:4.13',
      'truth': 'com.google.truth:truth:1.0.1',
      'guava': 'com.google.guava:guava:28.1-jre',
      'okio': 'com.squareup.okio:okio:2.8.0',
      'byteunits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'asm': 'org.ow2.asm:asm:9.0-beta',
      'diffUtils': 'io.github.java-diff-utils:java-diff-utils:4.7',
      'picnic': 'com.jakewharton.picnic:picnic:0.3.0',
  ]

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.0"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.10.0'
    classpath 'org.jlleitschuh.gradle:ktlint-gradle:9.3.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.9.0'
  }

  repositories {
    mavenCentral()
    jcenter()
    gradlePluginPortal()
  }
}

subprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  ktlint {
    version = '0.38.1'
    disabledRules = [
        'filename',
    ]
  }
  afterEvaluate {
    tasks.named('check').configure {
      it.dependsOn(tasks.getByName('ktlintCheck'))
    }
  }

  plugins.withId('org.jetbrains.kotlin.jvm') {
    compileKotlin {
      kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
            "-XXLanguage:+InlineClasses",
            "-Xjvm-default=enable",
            "-progressive",
            '-Xopt-in=kotlin.contracts.ExperimentalContracts'
        ]
      }
    }
  }
}
